`timescale 1ns / 1ps

module spi_master (
    
    input   logic           clk,
    input   logic           rst,
    
    
    input   logic           start,
    
    
    
    input   logic [7:0]     tx_data,
    output  logic           busy,    
    output  logic [7:0]     rx_data,
    output  logic           done,

    
    output  logic           sclk,
    output  logic           mosi,
    input   logic           miso,
    output  logic           ss_n
);
    typedef enum logic [1:0] {
        IDLE  = 2'b00,
        START,
        DATA,
        STOP
    } spi_state_e;
    spi_state_e state;

    localparam logic       FIXED_CPOL    = 1'b0;
    localparam logic       FIXED_CPHA    = 1'b0;
    localparam logic [7:0] FIXED_CLK_DIV = 8'd49;  


    logic [7:0] div_cnt;
    logic [7:0] clk_div_r;
    logic       half_tick;
    logic [7:0] tx_shift_reg;
    logic [7:0] rx_shift_reg;
    logic [2:0] bit_cnt;
    logic       step;
    logic       cpol_r;
    logic       cpha_r;
    logic       sclk_r;

    assign sclk = sclk_r;

    
    logic miso_d1, miso_d2;

    always_ff @(posedge clk or posedge rst) begin
        if (rst) begin
            {miso_d1, miso_d2} <= 2'b00;
        end 
        else begin
            {miso_d1, miso_d2} <= {miso, miso_d1};
        end
    end
    

    
    always_ff @(posedge clk or posedge rst) begin
        if (rst) begin
            div_cnt   <= 0;
            half_tick <= 1'b0;
        end else begin
            if (state == DATA) begin
                if (div_cnt == clk_div_r) begin
                    div_cnt   <= 0;
                    half_tick <= 1'b1;
                end else begin
                    div_cnt   <= div_cnt + 1;
                    half_tick <= 1'b0;
                end
            end else begin
                div_cnt   <= 0;
                half_tick <= 1'b0;
            end
        end
    end
    

    always_ff @(posedge clk or posedge rst) begin
        if (rst) begin
            state        <= IDLE;
            mosi         <= 1'b1;
            ss_n         <= 1'b1;
            busy         <= 1'b0;
            done         <= 1'b0;
            tx_shift_reg <= 0;
            rx_shift_reg <= 0;
            bit_cnt      <= 0;
            rx_data      <= 0;
            sclk_r       <= 0;
            
            cpol_r       <= 1'b0;
            clk_div_r    <= 0;
            step         <= 1'b0;
        end else begin
            done <= 1'b0;
            // Chip Select 제어와 8비트 송수신을 상태별로 순차 수행한다.
            case (state)
                IDLE : begin
                    mosi   <= 1'b1;
                    ss_n   <= 1'b1;
                    sclk_r <= 0;
                    if (start) begin
                        cpol_r    <= FIXED_CPOL;
                        cpha_r    <= FIXED_CPHA;
                        clk_div_r <= FIXED_CLK_DIV;
                        tx_shift_reg <= tx_data;
                        bit_cnt      <= 0;
                        busy         <= 1'b1;
                        step         <= 1'b0;
                        ss_n         <= 1'b0;
                        state        <= START;
                    end
                end
                START : begin
                    if (!cpha_r) begin
                        mosi         <= tx_shift_reg[7];
                        tx_shift_reg <= {tx_shift_reg[6:0], 1'b0};
                    end
                    state <= DATA;
                end
                DATA : begin
                    if (half_tick) begin
                        sclk_r <= ~sclk_r;
                        if (step == 0) begin    
                            step <= 1'b1;
                            if (!cpha_r) begin  
                                rx_shift_reg <= {rx_shift_reg[6:0], miso_d2};   
                            end
                            else begin          
                                mosi         <= tx_shift_reg[7];
                                tx_shift_reg <= {tx_shift_reg[6:0], 1'b0};
                            end
                        end else begin          
                            step <= 1'b0;
                            if (cpha_r) begin
                                rx_shift_reg <= {rx_shift_reg[6:0], miso_d2};   
                            end

                            if (bit_cnt < 7) begin
                                if (!cpha_r) begin  
                                    mosi         <= tx_shift_reg[7];
                                    tx_shift_reg <= {tx_shift_reg[6:0], 1'b0};
                                end
                                bit_cnt <= bit_cnt + 1;
                            end 
                            else begin              
                                state <= STOP;
                                if (cpha_r) begin   
                                    rx_data <= {rx_shift_reg[6:0], miso_d2};
                                end
                                else begin
                                    rx_data <= rx_shift_reg;
                                end
                            end
                        end
                    end
                end
                STOP : begin
                    sclk_r <= cpol_r;
                    ss_n   <= 1'b1;
                    done   <= 1'b1;
                    busy   <= 1'b0;
                    mosi   <= 1'b1;
                    state  <= IDLE;
                end
                default : state <= IDLE;
            endcase
        end
    end
endmodule
